# Maintainer: Sean Nelson <audiohacked@gmail.com>

pkgbase=jabberd
pkgname=jabberd2
pkgver=2.3.4
pkgrel=1
pkgdesc="Jabber (XMPP) Server"
arch=('i686' 'x86_64')
url="http://github.com/jabberd2/jabberd2"
license=("GPL")
depends=(
    'expat'
    'gsasl'
    'udns'
)

makedepends=(
    'libidn'
    'openssl'
    'zlib'
    'db'
    'openldap'
    'postgresql'
    'mariadb'
    'pam'
    'sqlite'
    'http-parser'
)

optdepends=(
    'libidn: needed for JID canonicalisation'
    'openssl: needed for SSL/TLS support'
    'zlib: needed for stream compression'
    'db: Berkley DB support'
    'openldap: LDAP support'
    'postgresql: PostgreSQL support'
    'mariadb: MySQL support'
    'pam: Linux PAM support'
    'sqlite: SQLite support'
    'http-parser'
)

source=(
    "https://github.com/jabberd2/jabberd2/releases/download/jabberd-${pkgver}/jabberd-${pkgver}.tar.xz"
    'jabberd2.install'
    'sysuser.conf'
)

install=jabberd2.install

# backup=('etc/jabberd2')

build() {
    cd $pkgbase-$pkgver
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc/jabberd2 \
	--localstatedir=/var/lib \
        --program-prefix=jabberd2- \
        --enable-ssl \
        --enable-mysql \
        --enable-pgsql \
        --enable-sqlite \
        --enable-db \
        --enable-ldap \
        --enable-pam \
        --enable-pipe \
        --enable-anon \
        --enable-fs \
        --enable-websocket \
        --without-subst
    make
}

package() {
    cd $pkgbase-$pkgver

    make DESTDIR="$pkgdir" install

    install -Dm644 $srcdir/sysuser.conf "$pkgdir"/usr/lib/sysusers.d/jabberd2.conf

}
md5sums=('8b7d76224b7ea62d5169065fa0879c83'
         'd2716a924c95b340b2bdc2fa9da33adc'
         '7eba015d39ae9aa4ef40ab931443a826')
