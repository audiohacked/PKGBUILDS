# Maintainer: Sean Nelson <audiohacked@gmail.com>
pkgname=cmangos-wotlk-git
pkgver=v0.17.r381.gfb81def
pkgrel=1
pkgdesc="Continued MaNGOS WotLK"
arch=('i686' 'x86_64')
url="https://github.com/cmangos/mangos-wotlk"
license=('GPL2')
depends=('pacman' 'mysql')
makedepends=('git' 'cmake')
conflicts=('cmangos')
provides=('cmangos')
install=cmangos-wotlk.install
source=("$pkgname"::'git://github.com/cmangos/mangos-wotlk.git'
	"ScriptDev2"::'git://github.com/scriptdev2/scriptdev2.git')
md5sums=('SKIP'
	'SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  # Use the tag of the last commit
  git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
	cd "${srcdir}/${pkgname}/src/bindings"
	git submodule add -f ${srcdir}/ScriptDev2 ScriptDev2

	mkdir -p "${srcdir}/build"
	cd "${srcdir}/build"
	cmake "${srcdir}/${pkgname}" -DCMAKE_INSTALL_PREFIX=/srv/mangos -DINCLUDE_BINDINGS_DIR=ScriptDev2
}

build() {
	cd "${srcdir}/build"
	make
}

package() {
	_sqlinstalldir="${pkgdir}/srv/mangos/sql"
	_cmangosdir="${srcdir}/${pkgname}"
	_scriptsdir="${srcdir}/${pkgname}/src/bindings/ScriptDev2"

	cd "${srcdir}/build"
	make DISTDIR="${pkgdir}" install

	install -Dm644 "${startdir}/cmangos-mangosd.service" "${pkgdir}/usr/lib/systemd/system/mangosd.service"
	install -Dm644 "${startdir}/cmangos-realmd.service" "${pkgdir}/usr/lib/systemd/system/realmd.service"

	install -Dm644 "${_cmangosdir}/sql/create_mysql.sql" \
		"${pkgdir}/srv/mangos/sql/cmangos_000.sql"

	install -Dm644 "${_scriptsdir}/sql/scriptdev2_create_database.sql" \
		"${pkgdir}/srv/mangos/sql/cmangos_001.sql"

	install -Dm644 "${_scriptsdir}/sql/scriptdev2_create_structure_mysql.sql" \
		"${pkgdir}/srv/mangos/sql/cmangos_002.sql"

	install -Dm644 "${_cmangosdir}/sql/mangos.sql" \
		"${pkgdir}/srv/mangos/sql/cmangos_003.sql"

	install -Dm644 "${_cmangosdir}/sql/characters.sql" \
		"${pkgdir}/srv/mangos/sql/cmangos_004.sql"

	install -Dm644 "${_cmangosdir}/sql/realmd.sql" \
		"${pkgdir}/srv/mangos/sql/cmangos_005.sql"

	install -Dm644 "${_scriptsdir}/sql/scriptdev2_script_full.sql" \
		"${pkgdir}/srv/mangos/sql/cmangos_101.sql"

}
